**********************************************************************
* <MWCBLCPY>                                                         *
* ALL RIGHTS RESERVED,COPYRIGHT(C)2003,                              *
* HIROYUKI KONISHI.                                                  *
* THIS PROGRAM IS A FREEWARE.                                        *
**********************************************************************
MWCBLCPY START 0
         PRINT NOGEN
         USING MWCBLCPY,R2,R3,R4,R5
         SAVE  (14,12)
         LR    R2,RF
         LA    RC,SAVER
         ST    RD,SAVER+4
         ST    RC,8(RD)
         LR    RD,RC
         LR    R3,R2
         LA    R3,2048(R3)
         LA    R3,2048(R3)
         LR    R4,R3
         LA    R4,2048(R4)
         LA    R4,2048(R4)
         LR    R5,R4
         LA    R5,2048(R5)
         LA    R5,2048(R5)
S00      EQU   *
         OPEN  (SYSIN,(INPUT),SYSLIN,(OUTPUT))
         OPEN  (SYSLIB,(INPUT))
         XR    R6,R6
         XR    R7,R7
         LH    R7,SYSLIB+62
         D     R6,=F'80'
         ST    R7,BF
         GETMAIN R,LV=800080
         ST    R0,GMSZ
         ST    R1,GMAD
S01      EQU   *
         GET   SYSIN,IAREA1
         CLC   IAREA1+10(6),=C' COPY '
         BNE   S05
         CLC   IAREA1+6(1),=C'*'
         BE    S05
         MVC   IAREA1+6(4),=C'****'
         PUT   SYSLIN,IAREA1
         MVC   KEY,IAREA1+16
         LA    R6,8
         LA    R7,KEY
S02      EQU   *
         CLC   0(1,R7),=C'.'
         BNE   S03
         MVC   0(1,R7),=C' '
S03      EQU   *
         LA    R7,1(R7)
         BCT   R6,S02
         L     R6,GMAD
         L     R7,=F'800080'
         L     R8,GMAD
         L     R9,=X'40000000'
         MVCL  R6,R8
         BAL   R6,T00
         L     R6,GMAD
S04      EQU   *
         CLC   0(80,R6),EDT
         BE    S01
         MVC   OAREA,0(R6)
         MVC   OAREA+72(8),=C'C       '
         PUT   SYSLIN,OAREA
         LA    R6,80(R6)
         B     S04
S05      EQU   *
         PUT   SYSLIN,IAREA1
         B     S01
T00      EQU   *
         L     R7,GMAD
         FIND  SYSLIB,KEY,D
         LTR   RF,RF
         BNZ   T03
T01      EQU   *
         LA    R8,IAREA2
         L     R9,=F'32760'
         LA    RA,IAREA2
         L     RB,=X'00000000'
         MVCL  R8,RA
         READ  DECBX,SF,SYSLIB,IAREA2
         CHECK DECBX
         L     R8,BF
         LA    R9,IAREA2
T02      EQU   *
         CLC   0(80,R9),LOW
         BE    T03
         MVC   0(80,R7),0(R9)
         LA    R7,80(R7)
         LA    R9,80(R9)
         BCT   R8,T02
         B     T01
T03      EQU   *
         MVC   0(80,R7),EDT
         BR    R6
EOF      EQU   *
         L     R1,GMAD
         FREEMAIN R,LV=800080,A=(R1)
         CLOSE (SYSIN,,SYSLIN,)
         CLOSE (SYSLIB,)
         L     RD,4(RD)
         RETURN (14,12),RC=0
SYSIN    DCB   DSORG=PS,MACRF=(GM),DDNAME=SYSIN,                       C
               EODAD=EOF,LRECL=80
SYSLIN   DCB   DSORG=PS,MACRF=(PM),DDNAME=SYSLIN,                      C
               RECFM=FB,LRECL=80,BLKSIZE=27920
SYSLIB   DCB   DSORG=PO,MACRF=(R),DDNAME=SYSLIB,                       C
               EODAD=T03
SAVER    DS    18F
GMSZ     DS    F
GMAD     DS    F
BF       DS    F
KEY      DC    CL8' '
EDT      DS    0CL80
         DC    80CL1'#'
LOW      DS    0CL80
         DC    80XL1'00'
IAREA1   DS    CL80
OAREA    DS    CL80
         LTORG
IAREA2   DS    CL32760
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
RA       EQU   10
RB       EQU   11
RC       EQU   12
RD       EQU   13
RE       EQU   14
RF       EQU   15
         END
